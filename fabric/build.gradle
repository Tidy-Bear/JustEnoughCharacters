plugins {
    id "fabric-loom"
    id 'java-library'
    id 'idea'
}

version = "${verspec}.${verbuild}"
group = 'me.towdium.jecharacters'

base {
    archivesName = mod_id + '-fabric'
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    compileOnly project(':common')
    implementation "com.github.towdium:PinIn:${verpinin}"
    include "com.github.Towdium:PinIn:${verpinin}"
    implementation project(":coremod-common")
    compileOnly "mezz.jei:jei-${minecraft_version}-core:${jei_version}"
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    compileOnly 'com.google.auto.service:auto-service-annotations:1.1.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
}

compileJava {
    source(project(":common").sourceSets.main.allSource)
    source(project(":coremod-common").sourceSets.main.allSource)
}

processResources {
    from project(":common").sourceSets.main.resources
    from project(":coremod-common").sourceSets.main.resources
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

apply from: "$rootDir/gradle/scripts/generate.gradle"

jar.dependsOn(generateTargetsJson)

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}